buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.8.0"
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4' apply false
	id 'io.spring.dependency-management' version '1.1.6' apply false
}

apply plugin: "com.vanniktech.dependency.graph.generator"

tasks.named('jar') {
	enabled = true
}

// 모든 모듈에 적용 (루트 - 서브 모듈에 공통 적용) 루트프로젝트도 포함
allprojects {

	group = 'com.streaming.settlement.system'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}
}

// 서브모듈에만 적용 (루트는 제외)
subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	// 공통 의존성
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

		implementation 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.4"
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	tasks.withType(Test) {
		enabled = false  // 테스트 실행을 비활성화
	}
}





